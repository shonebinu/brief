pkgdatadir = get_option('prefix') / get_option('datadir') / meson.project_name()
moduledir = pkgdatadir / 'brief'
gnome = import('gnome')

blueprints = custom_target('blueprints',
  input: files(
    'preferences.blp',
    'renderer.blp',
    'shortcuts-dialog.blp',
    'window.blp',
    'sidebar.blp',
  ),
  output: '.',
  command: [find_program('blueprint-compiler'), 'batch-compile', '@OUTPUT@', '@CURRENT_SOURCE_DIR@', '@INPUT@'],
)

gnome.compile_resources('brief',
  'brief.gresource.xml',
  gresource_bundle: true,
  install: true,
  install_dir: pkgdatadir,
  dependencies: blueprints,
)

python = import('python')

conf = configuration_data()
conf.set('PYTHON', python.find_installation('python3').full_path())
conf.set('VERSION', meson.project_version())
conf.set('localedir', get_option('prefix') / get_option('localedir'))
conf.set('pkgdatadir', pkgdatadir)

configure_file(
  input: 'brief.in',
  output: 'brief',
  configuration: conf,
  install: true,
  install_dir: get_option('bindir'),
  install_mode: 'rwxr-xr-x'
)

brief_sources = [
  '__init__.py',
  'main.py',
  'window.py',
  'tldr.py',
  'renderer.py',
  'preferences.py',
  'sidebar.py',
]

install_data(brief_sources, install_dir: moduledir)
